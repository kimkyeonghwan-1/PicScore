plugins {
	id 'java'
	id 'org.springframework.boot' version '3.4.3'
	id 'io.spring.dependency-management' version '1.1.7'
	id "org.sonarqube" version "4.0.0.2929"
	id 'jacoco'
}

jacoco {
    toolVersion = "0.8.10"
}

jacocoTestReport {
    reports {
        xml.required = true
        html.required = true
    }
    
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: [
                    "**/dto/**",
                    "**/entity/**",
                    "**/config/**",
                    "**/exception/**",
                    "**/*Application.java",
                    "**/constants/**"
            ])
        }))
    }
}

test {
    finalizedBy jacocoTestReport
    useJUnitPlatform()
}

sonarqube {
    properties {
        property "sonar.projectKey", "picscore-backend"
        property "sonar.projectName", "PicScore Backend"
        property "sonar.sources", "src/main"
        property "sonar.tests", "src/test"
        property "sonar.java.coveragePlugin", "jacoco"
        property "sonar.coverage.jacoco.xmlReportPaths", "${buildDir}/reports/jacoco/test/jacocoTestReport.xml"
        property "sonar.java.binaries", "${buildDir}/classes"
        
        // 테스트 관련 설정
        property "sonar.coverage.exclusions", "**/dto/**,**/entity/**,**/config/**,**/exception/**,**/*Application.java,**/constants/**"
        
        // 코드 중복 검사 제외 패턴
        property "sonar.cpd.exclusions", "**/dto/**,**/entity/**"
    }
}

group = 'com.picscore'
version = '0.0.1-SNAPSHOT'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(17)
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework:spring-webflux'
	implementation 'net.coobird:thumbnailator:0.4.8'

	compileOnly 'org.projectlombok:lombok'
	runtimeOnly 'com.mysql:mysql-connector-j'
	annotationProcessor 'org.projectlombok:lombok'

	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.security:spring-security-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

	//redis
	implementation 'org.springframework.boot:spring-boot-starter-data-redis'

	//jwt
	implementation 'io.jsonwebtoken:jjwt-api:0.12.3'
	implementation 'io.jsonwebtoken:jjwt-impl:0.12.3'
	implementation 'io.jsonwebtoken:jjwt-jackson:0.12.3'

	//json
	implementation 'com.fasterxml.jackson.core:jackson-databind:2.18.0'
	implementation 'com.fasterxml.jackson.core:jackson-core:2.18.0'
	implementation 'com.fasterxml.jackson.core:jackson-annotations:2.18.0'

	// s3
	implementation 'software.amazon.awssdk:s3:2.31.4'

	// health, prometheus
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'io.micrometer:micrometer-registry-prometheus'
}

// test 태스크는 위에서 이미 정의했으므로 제거

bootJar {
    archiveBaseName.set("app")  // bootJar의 이름을 'app'으로 설정
    archiveVersion.set("")      // 버전 정보 제거
}

// 기존 jar 태스크는 비활성화
jar {
    enabled = false
}